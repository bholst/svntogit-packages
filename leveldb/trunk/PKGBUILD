# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: feydaykyn <feydaykyn@yahoo.fr>

pkgname=leveldb
pkgver=1.22
pkgrel=3
pkgdesc="A fast and lightweight key/value database library"
arch=('x86_64')
url="https://github.com/google/leveldb"
license=('BSD')
depends=('snappy')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/google/leveldb/archive/$pkgver.tar.gz" "0002-disable-tcmalloc.patch")
sha1sums=('8d310af5cfb53dc836bfb412ff4b3c8aea578627'
          '0eeaa7a2b3891c64835b0b269c8d1036e948f434')

prepare() {
  cd "$pkgname-$pkgver"
  patch --forward --strip=1 --input="${srcdir}/0002-disable-tcmalloc.patch"
}

build() {
  mkdir -p "$pkgname-$pkgver/build"
  cd "$pkgname-$pkgver/build"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=1 \
    ..

  make
}

check() {
  make -C "$pkgname-$pkgver/build" test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir" install

  # documentation
  install -dm755 "$pkgdir"/usr/share/doc/"$pkgname"
  cp -dr --no-preserve=ownership doc/* "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
